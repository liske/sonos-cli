#!/usr/bin/perl

# sonos-cli - Command line interface to control 'Sonos ZonePlayer' 
#
# Authors:
#   Thomas Liske <thomas@fiasko-nw.net>
#
# Copyright Holder:
#   2010 - 2014 (C) Thomas Liske [http://fiasko-nw.net/~thomas/]
#
# License:
#   This program is free software; you can redistribute it and/or modify
#   it under the terms of the GNU General Public License as published by
#   the Free Software Foundation; either version 2 of the License, or
#   (at your option) any later version.
#
#   This program is distributed in the hope that it will be useful,
#   but WITHOUT ANY WARRANTY; without even the implied warranty of
#   MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
#   GNU General Public License for more details.
#
#   You should have received a copy of the GNU General Public License
#   along with this package; if not, write to the Free Software
#   Foundation, Inc., 51 Franklin St, Fifth Floor, Boston, MA  02110-1301 USA
#

use warnings;
use strict;

use AnyEvent;
use AnyEvent::Socket;
use AnyEvent::Handle;
use Data::Dumper;
use Getopt::Std;

$|++;
$Getopt::Std::STANDARD_HELP_VERSION++;

our $opt_h;
our $opt_p;
our $opt_i;
our $opt_o;

sub opt_default {
    $opt_h = '127.0.0.1';
    $opt_p = 1400;
    $opt_i = 'human';
    $opt_o = 'human';
}

opt_default;

sub HELP_MESSAGE {
    opt_default;

    print <<USG;
Usage:

  sosh [-h <host>] [-p <port>] [-i <format>] [-o <format>] [<script>]

    -h <host>	sonos-backend host [$opt_h]
    -p <port>   sonos-backend listen port [$opt_p]
    -i <format> input format ('human' or 'json') [$opt_i]
    -o <format> output format ('human' or 'json') [$opt_o]
    --help      show this help
    --version   show version information

    <script>    read commands from scripting file [/dev/stdin]

This is a sonos shell/interpreter to execute a batch of sonos
commands.

USG
}

sub VERSION_MESSAGE {
    print <<LIC;

sonos-cli $Net::UPnP::SONOS::VERSION - Command line interface to control 'Sonos ZonePlayer' 

Authors:
  Thomas Liske <thomas\@fiasko-nw.net>

Copyright Holder:
  2010 - 2014 (C) Thomas Liske [http://fiasko-nw.net/~thomas/]

Upstream:
  https://github.com/liske/sonos-cli

This program is free software; you can redistribute it and/or modify
it under the terms of the GNU General Public License as published by
the Free Software Foundation; either version 2 of the License, or
(at your option) any later version.

LIC
#/
}

unless(getopts('h:p:i:o:')) {
    HELP_MESSAGE;
    exit 1;
}

# read from script file
if(scalar @ARGV > 0) {
    close(STDIN);
    unless(open(STDIN, '<', $ARGV[0])) {
        print STDERR "Failed to read from '$ARGV[0]': $!\n";
        exit 1;
    }
}

my $htcp;
my $hstd;

my $fh;
tcp_connect $opt_h, $opt_p, sub {
    unless($fh = shift) {
	print STDERR "Unable to connect to $opt_h:$opt_p: $!\n";
	close(STDIN);
	exit 2;
    };

    $htcp = new AnyEvent::Handle(
	fh => $fh,
	on_eof => sub {
	    close(STDIN);
	    exit;
	},
	on_read => sub {
	    $htcp->push_read(json => sub {
		if($opt_o eq 'human') {
		    print Dumper($_[1]);
		}
		elsif($opt_o eq 'json') {
		    print $hstd->json_coder->encode($_[1])."\n";
		}
	    });
	}
    );

    $hstd = new AnyEvent::Handle(
	fh => \*STDIN,
	on_eof => sub {
	    $htcp->push_write("exit\n");
	},
	on_read => sub {
	    if($opt_i eq 'human') {
		$hstd->push_read(line => sub {
		    return if($_[1] =~ /^\s*#|^\s*$/);

		    print "> '$_[1]'\n";
		    $htcp->push_write("$_[1]\n");
		});
	    }
	    elsif($opt_i eq 'json') {
		$hstd->push_read(json => sub {
		    return if($_[1] =~ /^\s*#|^\s*$/);

		    $htcp->push_write($htcp->json_coder->encode($_[1]));
		});
	    }
	}
    );
};

AnyEvent->condvar->recv;
